select distinct ?iri (max(?lbl) as ?label) where
{
    {
        ?iri rdf:type owl:ObjectProperty
    }
    union
    {
        ?iri rdf:type owl:DatatypeProperty
    }
    union
    {
        ?iri rdf:type owl:AnnotationProperty
    }
    union
    {
        ?iri rdf:type rdf:Property
    }
    union
    {
        ?P rdfs:subPropertyOf ?iri
    }
    union
    {
        ?iri rdfs:subPropertyOf ?S
    }
    union
    {
        ?iri rdfs:domain ?D
    }
    union
    {
        ?iri rdfs:range ?P
    }

    optional
    {
        ?iri rdfs:label ?lbl
        filter(lang(?lbl) = "en" || lang(?lbl) = "")
    }
}
group by ?iri
